<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="F_WriteRepeatedField_1" Id="{663074d2-db41-414f-a82e-bae95a3038e4}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'estimated-stack-usage' := '4096'}
FUNCTION INTERNAL F_WriteRepeatedField_1 : HRESULT
VAR_IN_OUT
    stCtx  : ST_WriteContext;
END_VAR
VAR_INPUT CONSTANT
	iRepeatedField	: I_RepeatedField;
END_VAR

VAR
    nPackedDataSize : UDINT;
    i               : UDINT;
    hr              : HRESULT;
    anyItem         : T_Any;
	nTag			: UDINT;
	nEndTag			: UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF iRepeatedField.ipFieldCodec = 0  THEN
	F_WriteRepeatedField_1 := F_WriteError(stCtx, E_WriterErrorCode.InternalError);
    RETURN;
END_IF

IF iRepeatedField.Capcacity = 0 THEN
	RETURN;
END_IF

nTag := iRepeatedField.ipFieldCodec.Tag;
nEndTag := iRepeatedField.ipFieldCodec.EndTag;

IF iRepeatedField.ipFieldCodec.IsPackedRepeatedField(nTag)  THEN
    // packed: tag, length, and value
    
    hr := F_WriteRawVarint32(stCtx, nTag);
    IF FAILED(hr) THEN
        F_WriteRepeatedField_1 := hr;
        RETURN;
    END_IF
    
    nPackedDataSize := iRepeatedField.CalculatePackedDataSize();
    
    hr := F_WriteLength(stCtx, nPackedDataSize);
    IF FAILED(hr) THEN
        F_WriteRepeatedField_1 := hr;
        RETURN;
    END_IF
    
    IF iRepeatedField.Capcacity < 1 THEN
        RETURN;
    END_IF
    
    
    FOR i := 0 TO iRepeatedField.Capcacity - 1 DO
 		anyItem := iRepeatedField.ElementAtIndex(i);
        hr := iRepeatedField.ipFieldCodec.ValueWriter(stCtx, anyItem);
        IF FAILED(hr) THEN
            F_WriteRepeatedField_1 := hr;
            RETURN;
        END_IF
    END_FOR
    
ELSE    
   // Not packed: a simple tag/value pair for each value.
 
    FOR i := 0 TO iRepeatedField.Capcacity - 1 DO
		
		// Write Tag
        hr := F_WriteRawVarint32(stCtx, nTag);
        IF FAILED(hr) THEN
            F_WriteRepeatedField_1 := hr;
            RETURN;
        END_IF
        
 		anyItem := iRepeatedField.ElementAtIndex(i);
        hr := iRepeatedField.ipFieldCodec.ValueWriter(stCtx, anyItem);
        IF FAILED(hr) THEN
            F_WriteRepeatedField_1 := hr;
            RETURN;
        END_IF
		
		IF nEndTag <> 0 THEN
			hr := F_WriteRawVarint32(stCtx, nEndTag);
			IF FAILED(hr) THEN
				F_WriteRepeatedField_1 := hr;
				RETURN;
			END_IF			
		END_IF
    END_FOR
END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>