<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="F_ReadRepeatedField" Id="{3275937c-bc24-42b8-bf5d-84307b144b13}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION INTERNAL F_ReadRepeatedField : HRESULT
VAR_IN_OUT      
    stCtx : ST_ParseContext;
END_VAR
VAR_INPUT CONSTANT
	ipRepeatedField	: I_RepeatedField;
END_VAR

VAR
	nLastTag		: UDINT;
    hr          	: HRESULT;
    nLength     	: UDINT;
    nCurrentLimit 	: UDINT;
    bTagConsumed	: BOOL;
    nBufferEnd  	: UDINT;  
    anyValue        : T_Any;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF ipRepeatedField.ipFieldCodec = 0 THEN
    F_ReadRepeatedField := F_ParseError(stCtx,  E_ParserErrorCode.InternalError);
    RETURN;
END_IF

IF ipRepeatedField.nCapcacity = 0 THEN
	RETURN;
END_IF

nLastTag := stCtx.stState.nLastTag;

IF ipRepeatedField.ipFieldCodec.IsPackedRepeatedField(nLastTag) THEN
    
    hr := F_ParseLength(stCtx, nLength=> nLength);
    IF FAILED(hr) OR nLength <= 0 THEN
        // Empty packed field, valid - ignore.
        F_ReadRepeatedField := hr;
        RETURN;
    END_IF 
    
    F_SbhPushLimit(stCtx.stState, nLength, nCurrentLimit=> nCurrentLimit);
    nBufferEnd := stCtx.stState.nBufferPos + nLength;
    WHILE NOT F_SbhIsReachedLimit(stCtx.stState) DO
        hr := ipRepeatedField.ipFieldCodec.ValueReader(stCtx, anyOut=> anyValue);
        IF FAILED(hr) THEN
            F_ReadRepeatedField := hr;
            RETURN;
        END_IF
        
		hr := ipRepeatedField.SetValue(anyValue);
		IF FAILED(hr) THEN
            F_ReadRepeatedField := hr;
            RETURN;
        END_IF
    END_WHILE

    F_SbhPopLimit(stCtx.stState, nCurrentLimit);
ELSE
    // Not packed or possibly not packable...
    REPEAT
        hr := ipRepeatedField.ipFieldCodec.ValueReader(stCtx, anyOut=> anyValue);
        IF FAILED(hr) THEN
            F_ReadRepeatedField := hr;
            RETURN;
        END_IF
        
		hr := ipRepeatedField.SetValue(anyValue);
		IF FAILED(hr) THEN
            F_ReadRepeatedField := hr;
            RETURN;
        END_IF
        
        hr := F_MaybeConsumeTag(stCtx, nLastTag, bConsumed=> bTagConsumed);
        IF FAILED(hr) THEN
            F_ReadRepeatedField := hr;
            RETURN;
        END_IF
    UNTIL
        NOT bTagConsumed
    END_REPEAT
END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>