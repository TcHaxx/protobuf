<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_RepeatedField" Id="{6273a734-0b68-4ba6-b443-f02c4e67ae6b}" SpecialFunc="None">
    <Declaration><![CDATA[// Generic wrapper for ARRAY[*] OF T
FUNCTION_BLOCK FINAL FB_RepeatedField IMPLEMENTS I_RepeatedField
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	anyArrayField       : T_Any;
    anyFirstElemField   : T_Any;
    ipFieldCodecField	: I_FieldCodec;
END_VAR
VAR
	nCapacityField		: UDINT;
	nCountField			: UDINT;		// Actual number of elements in the Array
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="AddEntriesFrom" Id="{72f65244-7f13-453c-bcee-a20eaf48b6ec}">
      <Declaration><![CDATA[// Adds the entries from the given parse context, decoding them with the specified codec.
METHOD AddEntriesFrom : HRESULT
VAR_IN_OUT CONSTANT
	fbParseCtx	: FB_ParseContext;
END_VAR
VAR_INPUT
	ipFieldCodec : I_FieldCodec;
END_VAR
VAR_OUTPUT
	nCount : UDINT;
END_VAR
VAR
	refCtx	: REFERENCE TO ST_ParseContext;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[refCtx REF= fbParseCtx.refCtx;
THIS^.ipFieldCodecField := ipFieldCodec;
Initialize();
AddEntriesFrom := F_ReadRepeatedField(stCtx:= refCtx, ipRepeatedField:= THIS^, nLength=> nCount);]]></ST>
      </Implementation>
    </Method>
    <Method Name="CalculatePackedDataSize" Id="{90e54b0d-eaa3-492d-97d1-6ecfaa9fb248}">
      <Declaration><![CDATA[METHOD CalculatePackedDataSize : HRESULT
VAR_OUTPUT
    nPackedDataSize : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF ipFieldCodecField = NULL OR THIS^.anyFirstElemField.pValue = NULL THEN
	CalculatePackedDataSize := E_WriterErrorCode.InternalError;
	RETURN;
END_IF

IF THIS^.nCountField = 0 THEN
	nPackedDataSize := 0;
	RETURN;
END_IF 

CalculatePackedDataSize := ipFieldCodecField.CalculatePackedDataSize(
								anyFirstElem:= THIS^.anyFirstElemField,
								nLength:= THIS^.nCount,
								nPackedDataSize=> nPackedDataSize);]]></ST>
      </Implementation>
    </Method>
    <Method Name="CalculateSize" Id="{5892bb9f-d840-4406-badf-74aae82acf82}">
      <Declaration><![CDATA[METHOD CalculateSize : HRESULT
VAR_OUTPUT
	nSize : UDINT;
END_VAR
VAR
	nPackedSize : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF ipFieldCodecField = NULL OR THIS^.anyFirstElemField.pValue = NULL THEN
	CalculateSize := E_WriterErrorCode.InternalError;
	RETURN;
END_IF

IF THIS^.nCountField = 0 THEN
	nSize := 0;
	RETURN;
END_IF

IF ipFieldCodecField.IsPackedRepeatedField(ipFieldCodecField.Tag) THEN
	
	CalculateSize := CalculatePackedDataSize(nPackedDataSize=> nPackedSize);
	IF FAILED(CalculateSize) THEN
		RETURN;
	END_IF
	nSize := F_ComputeRawVarint32Size(ipFieldCodecField.Tag) + F_ComputeLengthSize(nPackedSize) + nPackedSize;
    RETURN;
END_IF

{warning 'TODO: Non packed repeated field'}
nSize := 0;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="ComputeCapacity" Id="{afe8a243-10c9-425f-b110-73d6acc35475}">
      <Declaration><![CDATA[METHOD PRIVATE ComputeCapacity : UDINT
VAR_INPUT
END_VAR
VAR
	nTotalSize		: UDINT;
	nElementSize	: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
nTotalSize := TO_UDINT(anyArrayField.diSize);
IF nTotalSize = 0 THEN
	RETURN;
END_IF

nElementSize := TO_UDINT(anyFirstElemField.diSize);
IF nElementSize = 0 THEN
	RETURN;
END_IF

ComputeCapacity := nTotalSize / nElementSize;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ElementAtIndex" Id="{0c3f4543-a722-41c0-84ce-92f47960c155}">
      <Declaration><![CDATA[METHOD ElementAtIndex : T_ANY
VAR_INPUT
	nIndex	: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ElementAtIndex := THIS^.anyFirstElemField;

IF nIndex > nCapacityField - 1 THEN
	RETURN;
END_IF

ElementAtIndex := THIS^.anyFirstElemField;
ElementAtIndex.pValue := THIS^.anyFirstElemField.pValue + (nIndex * TO_UDINT(THIS^.anyFirstElemField.diSize)); ]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_Init" Id="{177fec20-a9cf-4d77-9f17-44aa33f5a0d5}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)   
	anyArray        : T_Any;
    anyFirstElem    : T_Any;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[anyArrayField       := anyArray;
anyFirstElemField   := anyFirstElem;

Initialize();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{16109bc0-6856-479b-b08c-db0a5f544e10}">
      <Declaration><![CDATA[METHOD Initialize : HRESULT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
nCapacityField := ComputeCapacity();
nCountField 	:= 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="InitializeFromArray" Id="{253eea7f-9927-4d4d-be14-2b1ec6c2f700}">
      <Declaration><![CDATA[METHOD InitializeFromArray : HRESULT
VAR_INPUT
	anyArray        : T_Any;
    anyFirstElem    : T_Any;
	nCount			: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[anyArrayField       := anyArray;
anyFirstElemField   := anyFirstElem;

Initialize();
IF nCount <= THIS^.nCapacityField THEN
	THIS^.nCountField := nCount;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="ipFieldCodec" Id="{af641ca7-e4a7-4470-a085-a12dba67de50}">
      <Declaration><![CDATA[PROPERTY ipFieldCodec : I_FieldCodec
]]></Declaration>
      <Get Name="Get" Id="{4dfee661-3c43-41f7-a9f8-05276ffe98a1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ipFieldCodec := ipFieldCodecField;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="nCapcacity" Id="{02f68e37-5912-4996-9507-87a873524f1a}">
      <Declaration><![CDATA[PROPERTY nCapcacity : UDINT
]]></Declaration>
      <Get Name="Get" Id="{fb1cafc4-00dd-480e-ac47-5aefc582e3fe}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nCapcacity := nCapacityField;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="nCount" Id="{935b9e97-e1dc-4e85-a299-be6bfd0e0e76}">
      <Declaration><![CDATA[PROPERTY nCount : UDINT
]]></Declaration>
      <Get Name="Get" Id="{7f8827ea-e9cd-4310-9284-d87e61bb67ae}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nCount := THIS^.nCountField;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{c305c29b-ef8e-4865-855b-a8c166b4bd19}">
        <Declaration><![CDATA[
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.nCountField := MIN(nCount, THIS^.nCapacityField);]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="SetValue" Id="{4d35c2f2-eb7c-4eeb-82f5-c3ea1a9f1a96}">
      <Declaration><![CDATA[// Sets the given value at the current element and increases nCount
METHOD SetValue : HRESULT
VAR_INPUT
	anyValue	: T_ANY;
END_VAR
VAR
	anyCurrentElem : T_ANY;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF THIS^.nCountField > THIS^.nCapacityField THEN
	SetValue := E_ParserErrorCode.NotEnoughMem;
	RETURN;
END_IF

anyCurrentElem := THIS^.ElementAtIndex(THIS^.nCountField);

MEMCPY(destAddr:= anyCurrentElem.pValue, srcAddr:= anyValue.pValue, n:= TO_UDINT(MIN(anyCurrentElem.diSize, anyValue.diSize)));
 
THIS^.nCountField := THIS^.nCountField + 1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteTo" Id="{13998a98-9f58-46b4-b17a-76b3ffdca256}">
      <Declaration><![CDATA[// Writes the contents of this collection to the given write context, encoding each
// value using the specified codec.
METHOD WriteTo : HRESULT
VAR_IN_OUT CONSTANT
	fbWriteCtx	: FB_WriteContext;
END_VAR
VAR_INPUT
	ipFieldCodec : I_FieldCodec;
END_VAR
VAR
	refCtx	: REFERENCE TO ST_WriteContext;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[refCtx REF= fbWriteCtx.refCtx;
THIS^.ipFieldCodecField := ipFieldCodec;
WriteTo := F_WriteRepeatedField(stCtx:= refCtx, ipRepeatedField:= THIS^);]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteToWithCount" Id="{600a3a4c-94da-405a-ab74-04f185c0bde4}">
      <Declaration><![CDATA[// Writes the contents of this collection to the given write context, encoding each
// value using the specified codec.
METHOD WriteToWithCount : HRESULT
VAR_IN_OUT CONSTANT
	fbWriteCtx	: FB_WriteContext;
END_VAR
VAR_INPUT
	ipFieldCodec : I_FieldCodec;
	nCount		 : UDINT;			// Number of items in the collection.
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.nCount := nCount;
WriteToWithCount := WriteTo(fbWriteCtx, ipFieldCodec);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>