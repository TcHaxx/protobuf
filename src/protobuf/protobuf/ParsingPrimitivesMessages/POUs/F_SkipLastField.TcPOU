<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="F_SkipLastField" Id="{1b7929cf-3775-43d5-ada9-c6ec50538adc}" SpecialFunc="None">
    <Declaration><![CDATA[// Reading and skippig messages / groups
FUNCTION INTERNAL F_SkipLastField : HRESULT
VAR_IN_OUT      
    stCtx : ST_ParseContext;
END_VAR
VAR
    nLength : UDINT;
    nDummy  : UDINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
IF stCtx.stState.nLastTag = 0 THEN
    F_SkipLastField := F_ParseError(stCtx, E_ParserErrorCode.EndOfStream);
    RETURN;
END_IF

CASE F_GetTagWireType(stCtx.stState.nLastTag) OF
    E_WireType.StartGroup,
    E_WireType.EndGroup:
        F_SkipLastField := F_ParseError(stCtx, E_ParserErrorCode.NotImplemented);

    E_WireType.Fixed32:
        F_SkipLastField := F_ParseRawLittleEndian32(stCtx, nValue=> );
       
    E_WireType.Fixed64:
        F_SkipLastField := F_ParseRawLittleEndian64(stCtx, nValue=> );
       
    E_WireType.LengthDelimited:
        F_SkipLastField := F_ParseLength(stCtx, nLength=> nLength);
        IF SUCCEEDED(F_SkipLastField) THEN
            F_SkipLastField := F_SkipRawBytes(nLength, stCtx.stState);
        END_IF
    
    E_WireType.Varint:
        F_SkipLastField := F_ParseRawVarint32(stCtx, nDummy);
ELSE
    F_SkipLastField := F_ParseError(stCtx, E_ParserErrorCode.InternalError);
    RETURN;
END_CASE]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>