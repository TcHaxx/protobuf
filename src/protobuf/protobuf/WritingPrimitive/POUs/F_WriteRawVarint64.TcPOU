<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="F_WriteRawVarint64" Id="{529ba7fd-604b-4712-80e6-c429e7431e0d}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION F_WriteRawVarint64 : HRESULT
VAR_IN_OUT      
    stCtx : ST_WriteContext;
END_VAR
VAR_INPUT
    anyVarInt64 : ANY_INT;
END_VAR
VAR
    nValue  : ULINT;
    pByte   : POINTER TO BYTE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
IF anyVarInt64.diSize > SIZEOF(nValue) THEN
    F_WriteRawVarint64 := stCtx.stState.eWriterErrorCode := E_WriterErrorCode.InvalidDataType;     
    stCtx.stState.bError := TRUE;
    RETURN; 
END_IF

// Enough capacity left for theoretical max Varint64?
IF (stCtx.stState.nBufferPos + SIZEOF(nValue) + 1) > stCtx.stState.nBufferSize THEN
    F_WriteRawVarint64 := stCtx.stState.eWriterErrorCode := E_WriterErrorCode.TruncatedMessage;     
    stCtx.stState.bError := TRUE;
    RETURN; 
END_IF

nValue := PULINT_TO_ULINT(in:= anyVarInt64.pValue);

pByte := stCtx.stBuffer.pBuffer + stCtx.stState.nBufferPos;

// fastpath for a common case of single byte value
IF nValue < 16#80 THEN
    pByte^ := TO_BYTE(nValue);
    stCtx.stState.nBufferPos := stCtx.stState.nBufferPos + 1;
    F_WriteRawVarint64 := S_OK;
    RETURN;
END_IF

WHILE (stCtx.stState.nBufferPos < stCtx.stState.nBufferSize) DO
    pByte := stCtx.stBuffer.pBuffer + stCtx.stState.nBufferPos;
    IF nValue > 16#7F THEN
        pByte^ := TO_BYTE((nValue AND 16#7F) OR 16#80);
        nValue := SHR(nValue, 7);
        stCtx.stState.nBufferPos := stCtx.stState.nBufferPos + 1;
    ELSE
        pByte^ := TO_BYTE(nValue);
        stCtx.stState.nBufferPos := stCtx.stState.nBufferPos + 1;
        F_WriteRawVarint64 := S_OK;
        RETURN;
    END_IF
END_WHILE

// should never reach
F_WriteRawVarint64 := E_WriterErrorCode.InternalError;]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>