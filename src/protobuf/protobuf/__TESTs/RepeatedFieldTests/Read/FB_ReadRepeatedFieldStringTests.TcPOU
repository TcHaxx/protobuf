<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_ReadRepeatedFieldStringTests" Id="{481fde89-a9de-4d33-b2f7-5458b704ebce}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK INTERNAL FINAL FB_ReadRepeatedFieldStringTests EXTENDS FB_TestSuite
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
    fbFieldCodecString : FB_FieldCodecString;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[ShouldReadRepeatedFieldTheory();
ShouldReadNonConsecutiveRepeatedFields(); ]]></ST>
    </Implementation>
    <Method Name="ShouldReadNonConsecutiveRepeatedFields" Id="{52cb7e82-bd02-4a86-a734-0d7e1470d8fb}">
      <Declaration><![CDATA[METHOD PRIVATE ShouldReadNonConsecutiveRepeatedFields
VAR CONSTANT
    (*
        message Demo {
            repeated uint32 a = 1;
            fixed32 b =2;
        }
        JSON:
        {
          "a": [
            0, 1, 2, 151
          ],
          "b": 321
        }
    *)
    cRawMessage : ARRAY[0..15] OF BYTE := [16#0A, 16#03, 16#00, 16#01, 16#02, 16#15, 16#41, 16#01, 16#00, 16#00, 16#0A, 16#02, 16#97, 16#01];
    cExpected   : ARRAY[0..15] OF BYTE := [0, 1, 2, 151];
END_VAR
VAR_INST
    fbCtx               : FB_ParseContext;
    fbFieldCodecInt32 : FB_FieldCodecInt32;
END_VAR
VAR
    aActual             : ARRAY [0..15] OF BYTE;
    hr                  : HRESULT;
    nActualFieldIndex   : UDINT;
    nB                  : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

// Arrange
fbCtx.FromBuffer(cRawMessage);

// Act
// Parses a=[0,1,2]: 16#0A, 16#03, 16#00, 16#01, 16#02
F_ParseTag(fbCtx.refCtx, nTag=> );
hr := F_ReadRepeatedField(fbCtx.refCtx, anyArray:= F_ToAnyType(aActual), anyFirstElem:= F_ToAnyType(aActual[0]), ipFieldCodec:= fbFieldCodecInt32, nFieldIndex:= nActualFieldIndex);

// Parses b=321: 16#15, 16#41, 16#01, 16#00, 16#00
F_ParseTag(fbCtx.refCtx, nTag=> );
F_ParseRawLittleEndian32(fbCtx.refCtx, nValue=> nB);

// Parses a=[151]: 16#0A, 16#02, 16#97, 16#01
F_ParseTag(fbCtx.refCtx, nTag=> );
hr := F_ReadRepeatedField(fbCtx.refCtx, anyArray:= F_ToAnyType(aActual), anyFirstElem:= F_ToAnyType(aActual[0]), ipFieldCodec:= fbFieldCodecInt32, nFieldIndex:= nActualFieldIndex);

// Assert
AssertEquals_DINT(
    Expected:= S_OK,
    Actual:= hr,
    Message:= 'Expected S_OK instead of error');
    
{warning disable C0041}
AssertArrayEquals_BYTE(Expecteds:= cExpected, Actuals:= aActual, Message:= '');
    
AssertEquals_UDINT(
    Expected:= 4,
    Actual:= nActualFieldIndex,
    Message:= CONCAT('Expected nActualFieldIndex to equal ', TO_STRING(4)));
    
TEST_FINISHED_NAMED(__POUNAME());]]></ST>
      </Implementation>
    </Method>
    <Method Name="ShouldReadRepeatedField" Id="{ea7665fd-02f2-480a-9007-efe05cdec4ed}">
      <Declaration><![CDATA[METHOD PRIVATE ShouldReadRepeatedField
VAR_IN_OUT CONSTANT
    aBuffer     : ARRAY [*] OF BYTE;
    aExpected   : ARRAY [*] OF T_MAXSTRING;
END_VAR
VAR_INPUT CONSTANT
    nBufferPosStart     : UDINT;
    nExpectedBufferPos  : UDINT;
    nExpectedFieldIndex : UDINT;
    ipFieldCodec        : I_FieldCodec;
END_VAR
VAR_INST
    fbCtx               : FB_ParseContext;
END_VAR
VAR
    aActual             : ARRAY[0..15] OF T_MAXSTRING;
    hr                  : HRESULT;
    nActualFieldIndex   : UDINT;
    nTag                : UDINT;
    i                   : DINT;
    aTmpBuffer          : ARRAY[0..1023] OF BYTE := [SIZEOF(aTmpBuffer)(16#FF)];
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Arrange
fbCtx.FromBuffer(aBuffer);
fbCtx.refCtx.stState.nBufferPos := nBufferPosStart;

THIS^.fbFieldCodecString.SetTmpBuffer(anyString:= F_ToAnyBytes(aTmpBuffer));

// Act
F_ParseTag(fbCtx.refCtx, nTag=> nTag);
hr := F_ReadRepeatedField(fbCtx.refCtx, anyArray:= F_ToAnyType(aActual), anyFirstElem:= F_ToAnyType(aActual[0]), ipFieldCodec, nFieldIndex:= nActualFieldIndex);

// Assert
AssertEquals_DINT(
    Expected:= S_OK,
    Actual:= hr,
    Message:= 'Expected S_OK instead of error');
    
{warning disable C0041}
// AssertArrayEquals_(Expecteds:= aExpected, Actuals:= aActual, Message:= '');
FOR i := 0  TO UPPER_BOUND(aExpected, 1) DO 
    AssertEquals_STRING(Expected:= aExpected[i], Actual:= aActual[i], Message:= '');
END_FOR

AssertEquals_UDINT(
    Expected:= nExpectedBufferPos,
    Actual:= fbCtx.refCtx.stState.nBufferPos,
    Message:= CONCAT('Expected stState.nBufferPos to equal ', TO_STRING(nExpectedBufferPos)));
    
AssertEquals_UDINT(
    Expected:= nExpectedFieldIndex,
    Actual:= nActualFieldIndex,
    Message:= CONCAT('Expected nActualFieldIndex to equal ', TO_STRING(nExpectedFieldIndex)));
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ShouldReadRepeatedFieldTheory" Id="{e5fc16b6-2350-4421-bb6e-d4de18745678}">
      <Declaration><![CDATA[METHOD PRIVATE ShouldReadRepeatedFieldTheory : REFERENCE TO FB_Test
VAR CONSTANT
    cRawValueString1    : ARRAY[0..255] OF BYTE := [16#0A, 16#0B, 16#54, 16#65, 16#73, 16#74, 16#53, 16#74, 16#72, 16#69, 16#6E, 16#67, 16#30, 16#0A, 16#0B, 16#54, 16#65, 16#73, 16#74, 16#53, 16#74, 16#72, 16#69, 16#6E, 16#67, 16#31, 16#0A, 16#0B, 16#54, 16#65, 16#73, 16#74, 16#53, 16#74, 16#72, 16#69, 16#6E, 16#67, 16#32, 16#0A, 16#0B, 16#54, 16#65, 16#73, 16#74, 16#53, 16#74, 16#72, 16#69, 16#6E, 16#67, 16#33];
    
END_VAR

VAR CONSTANT
    aExpectedString1        : ARRAY[0..15] OF T_MAXSTRING := ['TestString0', 'TestString1', 'TestString2', 'TestString3'];
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

ShouldReadRepeatedField(aBuffer:= cRawValueString1, aExpected:= aExpectedString1, nBufferPosStart:= 0, nExpectedBufferPos:= 53, nExpectedFieldIndex:= 4, THIS^.fbFieldCodecString);

TEST_FINISHED_NAMED(__POUNAME());]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>