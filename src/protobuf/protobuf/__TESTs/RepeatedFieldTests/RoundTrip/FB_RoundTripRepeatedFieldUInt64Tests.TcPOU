<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_RoundTripRepeatedFieldUInt64Tests" Id="{d9f017a1-d824-4d5e-a676-2adbb8307474}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK INTERNAL FINAL FB_RoundTripRepeatedFieldUInt64Tests EXTENDS FB_TestSuite
VAR CONSTANT
    cExpected 			: ARRAY[0..7] OF ULINT := [0, 42, 6969, 1337, 16#FFFFFFFFFFFFFFFF];
	cExpectedFieldIndex : UDINT := 5;
END_VAR
VAR
    aBuffer         	: ARRAY[0..255] OF BYTE;
    hrWriterActual  	: HRESULT;
    hrParserActual  	: HRESULT;
	aActuals,aValues	: ARRAY[0..7] OF ULINT ;
	nActualTag			: UDINT;
	nExpectedTag	    : UDINT;
	fbParseCtx_SUT      : FB_ParseContext;
	fbWriteCtx_SUT      : FB_WriteContext;
	fbFieldCodec 		: FB_FieldCodecUint64(nTag:= F_MakeTag(nFieldNumber:= 3, eWireType:= E_WireType.LengthDelimited));
	fbRepeatedField_Write : FB_RepeatedField(anyArray:= F_ToAnyType(aValues), anyFirstElem:= F_ToAnyType(aValues[0]));
	fbRepeatedField_Parse : FB_RepeatedField(anyArray:= F_ToAnyType(aActuals), anyFirstElem:= F_ToAnyType(aActuals[0]));	
END_VAR
VAR_TEMP
	i : INT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[TEST(__POUNAME());

// Arrange
nExpectedTag := F_MakeTag(nFieldNumber:= 3, eWireType:= E_WireType.LengthDelimited);
aValues := cExpected;
fbRepeatedField_Write.InitializeFromArray(anyArray:= F_ToAnyType(aValues), anyFirstElem:= F_ToAnyType(aValues[0]), nCount:= 5);
fbWriteCtx_SUT.FromBuffer(aBuffer);
fbParseCtx_SUT.FromBuffer(aBuffer);

// Act

hrWriterActual := fbRepeatedField_Write.WriteTo(fbWriteCtx_SUT, fbFieldCodec);
fbParseCtx_SUT.ReadTag(nTag=> nActualTag);
hrParserActual := fbRepeatedField_Parse.AddEntriesFrom(fbParseCtx_SUT, fbFieldCodec);

// Assert

AssertEquals_DINT(Expected:= S_OK, Actual:= hrWriterActual, Message:= 'Expected hrWriterActual = S_OK');
AssertEquals_DINT(Expected:= S_OK, Actual:= hrParserActual, Message:= 'Expected hrParserActual = S_OK');
AssertEquals_UDINT(Expected:= nExpectedTag, Actual:= nActualTag, Message:= 'Expeced nActualTag to equal nExpectedTag.');
AssertEquals_UDINT(Expected:= cExpectedFieldIndex, Actual:= fbRepeatedField_Parse.nCount, Message:= 'Expeced fbRepeatedField_Parse.nCount to equal cExpectedFieldIndex.');
{warning disable C0041}
AssertArrayEquals_ULINT(Expecteds:= cExpected, Actuals:= aActuals, Message:= '');

TEST_FINISHED_NAMED(__POUNAME());]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>