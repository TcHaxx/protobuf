<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_WriteRepeatedFieldInt64Tests" Id="{2409569a-f6cb-4426-80b6-a0635b06f7ab}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK INTERNAL FINAL FB_WriteRepeatedFieldInt64Tests EXTENDS FB_TestSuite
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	fbFieldCodec : FB_FieldCodecInt64(nTag:= 0);
	aActual    	 : ARRAY [0..7] OF LINT;
	fbRepeatedField		: FB_RepeatedField(anyArray:= F_ToAnyType(aActual), anyFirstElem:= F_ToAnyType(aActual[0]));
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[ShouldWriteRepeatedFieldTheory();]]></ST>
    </Implementation>
    <Method Name="Reset" Id="{0b55874f-2ee4-408d-95dc-a307b8a24e21}">
      <Declaration><![CDATA[METHOD PRIVATE Reset
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbRepeatedField.Initialize();
MEMSET(destAddr:= ADR(aActual), fillByte:= 16#0, n:= SIZEOF(aActual));]]></ST>
      </Implementation>
    </Method>
    <Method Name="ShouldWriteRepeatedField" Id="{a17e1d39-2bab-4008-be2e-855113822c5a}">
      <Declaration><![CDATA[METHOD PRIVATE ShouldWriteRepeatedField
VAR_IN_OUT CONSTANT
    aExpected   : ARRAY [*] OF BYTE;
END_VAR
VAR_INPUT
    aValues     : ARRAY [0..7] OF LINT;
    nLength     : UDINT;
    nTag        : UDINT;
    nBufferPosStart     : UDINT;
    nExpectedBufferPos  : UDINT;
    ipFieldCodec        : I_FieldCodec;
END_VAR
VAR_INST
    fbCtx   : FB_WriteContext;
END_VAR
VAR
    aActualBuffer       : ARRAY [0..63] OF BYTE;
    hr                  : HRESULT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Arrange
Reset();
fbCtx.FromBuffer(aActualBuffer);
fbCtx.refCtx.stState.nBufferPos := nBufferPosStart;
ipFieldCodec.Tag := nTag;
fbRepeatedField.InitializeFromArray(anyArray:= F_ToAnyType(aValues), anyFirstElem:= F_ToAnyType(aValues[0]), nCount:= nLength);

// Act
hr := fbRepeatedField.WriteTo(fbWriteCtx:= fbCtx, ipFieldCodec:= ipFieldCodec);

// Assert
AssertEquals_DINT(
    Expected:= S_OK,
    Actual:= hr,
    Message:= 'Expected S_OK instead of error');
    
{warning disable C0041}
AssertArrayEquals_BYTE(Expecteds:= aExpected, Actuals:= aActualBuffer, Message:= '');
 
AssertEquals_UDINT(
    Expected:= nExpectedBufferPos,
    Actual:= fbCtx.refCtx.stState.nBufferPos,
    Message:= CONCAT('Expected stState.nBufferPos to equal ', TO_STRING(nExpectedBufferPos)));

]]></ST>
      </Implementation>
    </Method>
    <Method Name="ShouldWriteRepeatedFieldTheory" Id="{833b0050-278e-497e-ab72-f9898ffc354b}">
      <Declaration><![CDATA[METHOD PRIVATE ShouldWriteRepeatedFieldTheory : REFERENCE TO FB_Test
VAR CONSTANT
    cRawValueZeroLength     : ARRAY[0..63] OF BYTE := [16#00, 16#00];
    cRawValue1              : ARRAY[0..63] OF BYTE := [16#00, 16#0A, 16#18, 16#00, 16#D2, 16#09, 16#B9, 16#0A, 16#FF, 16#FF, 16#FF, 16#FF, 16#FF, 16#FF, 16#FF, 16#FF, 16#FF, 16#01, 16#CE, 16#C2, 16#F1, 16#05, 16#B4, 16#80, 16#80, 16#80, 16#08];  
END_VAR

VAR CONSTANT
    aExpectedZeroLength     : ARRAY [0..7] OF LINT := [0, 0, 0, 0, 0, 0, 0, 0];
    aExpected1              : ARRAY [0..7] OF LINT := [0, 1234, 1337, -1, 12345678, 2147483700];
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

ShouldWriteRepeatedField(aExpected:= cRawValueZeroLength, aValues:= aExpectedZeroLength, nLength:= 0, nTag:= F_MakeTag(1, E_WireType.LengthDelimited), nBufferPosStart:= 0, nExpectedBufferPos:= 0, ipFieldCodec:= THIS^.fbFieldCodec);
ShouldWriteRepeatedField(aExpected:= cRawValue1, aValues:= aExpected1, nLength:= 6, nTag:= F_MakeTag(1, E_WireType.LengthDelimited), nBufferPosStart:= 1, nExpectedBufferPos:= 27, ipFieldCodec:= THIS^.fbFieldCodec);

TEST_FINISHED_NAMED(__POUNAME());]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>