<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_FieldCodecSint32" Id="{5872bbd0-7f0f-48ad-9c30-e5bb23d85c6b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_FieldCodecSint32 IMPLEMENTS I_FieldCodec
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
    nValue : DINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="CalculatePackedDataSize" Id="{edce7463-02fa-4e5d-a669-d8e25f703234}">
      <Declaration><![CDATA[METHOD CalculatePackedDataSize : HRESULT
VAR_INPUT CONSTANT
	anyFirstElem	: T_Any;
	nLength	        : UDINT;        // items to write
END_VAR
VAR_OUTPUT
    nPackedDataSize : UDINT;
END_VAR
VAR
    anyItem         : T_ANY;
    i               : UDINT;
    nValue          : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[anyItem := anyFirstElem;

IF nLength < 1 THEN
    nPackedDataSize := 0;
    RETURN;
END_IF

FOR i := 0  TO nLength - 1 DO
    anyItem.pValue := anyFirstElem.pValue + (i * TO_UDINT(anyFirstElem.diSize)); 
    nValue := PDINT_TO_DINT(anyItem.pValue);
    nPackedDataSize := nPackedDataSize + F_ComputeSInt32Size(nValue);
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsPackedRepeatedField" Id="{495b96ec-f670-4571-942b-2d9493d39e45}">
      <Declaration><![CDATA[METHOD IsPackedRepeatedField : BOOL
VAR_INPUT
	nTag	: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsPackedRepeatedField := F_IsPackedRepeatedField(nTag);]]></ST>
      </Implementation>
    </Method>
    <Method Name="ValueReader" Id="{0fb52bb4-2627-4e4a-8cf4-f8046717ea94}">
      <Declaration><![CDATA[
METHOD ValueReader : HRESULT
VAR_IN_OUT      
    stCtx : ST_ParseContext;
END_VAR
VAR_OUTPUT
	anyOut	: T_Any;
END_VAR
VAR
    nTmp : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ValueReader := F_ParseRawVarint32(stCtx, nTmp);
IF FAILED(ValueReader) THEN
    RETURN;
END_IF
nValue := F_DecodeZigZag32(nTmp);
anyOut := F_ToAnyType(anyArg:= nValue);]]></ST>
      </Implementation>
    </Method>
    <Method Name="ValueWriter" Id="{0f7622f3-c0bf-441a-92f1-2aafa8694ea1}">
      <Declaration><![CDATA[METHOD ValueWriter : HRESULT
VAR_IN_OUT
    stCtx  : ST_WriteContext;
END_VAR
VAR_INPUT
    anyIn : T_ANY;
END_VAR
VAR
    nValue  : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
nValue := PDINT_TO_DINT(anyIn.pValue);
ValueWriter := F_WriteSInt32(stCtx, nValue);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>