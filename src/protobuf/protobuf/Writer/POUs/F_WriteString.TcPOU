<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="F_WriteString" Id="{09fd94e5-4952-4576-a108-1cdf6abd18f4}" SpecialFunc="None">
    <Declaration><![CDATA[// Writes a string field value, without a tag, to the stream. The data is length-prefixed.
FUNCTION F_WriteString : HRESULT
VAR_IN_OUT
    aBuffer : ARRAY [*] OF BYTE;
    stState : ST_WriterInternalState;
END_VAR
VAR_IN_OUT CONSTANT
    sValue  : T_ProtobufMaxString;
END_VAR
VAR
    nStrLength : UDINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[F_WriteString := S_OK;

UTF8Len(ADR(sValue), nSize=> nStrLength);
F_WriteString := F_WriteRawVarint32(aBuffer, stState, nStrLength);
IF FAILED(F_WriteString) THEN
    RETURN;
END_IF

IF nStrLength > stState.nBufferSize - stState.nBufferPos THEN
    F_WriteString := stState.eWriterErrorCode := E_WriterErrorCode.TruncatedMessage;
    stState.bError := TRUE;
    RETURN;  
END_IF

MEMCPY(destAddr:= ADR(aBuffer[stState.nBufferPos]), srcAddr:= ADR(sValue), n:=  nStrLength);
stState.nBufferPos := stState.nBufferPos + nStrLength;]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>